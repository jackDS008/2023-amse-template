transform CelsiusToFahrenheit {
        from temperatureCelsius oftype decimal;
        to temperatureFahrenheit oftype decimal;
        temperatureFahrenheit: ((temperatureCelsius * 9/5) + 32);
    }

pipeline TemperaturePipeline {

    block TemperatureExtractor oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    block TempArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block TempFilePicker oftype FilePicker {
        path: "/data.csv";
    }

    block TempFileInterpreter oftype TextFileInterpreter{
        
    }

    block TempCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    block TempColumnDeleter oftype ColumnDeleter {
        delete: [column F, column G, column H, column I,  column L, column M, column N, column O];
    }

    block TempTableInterpreter oftype TableInterpreter {
        header: false;
        columns: [
            "Geraet" oftype integer,
            "Hersteller" oftype text,
            "Model" oftype text,
            "Monat" oftype integer,
            "Temperatur" oftype decimal,
            "Batterietemperatur" oftype decimal,
            "Geraet aktiv" oftype text,
        ];
    }

    block TemperatureTableTransformer oftype TableTransformer {
        inputColumn: 'Temperatur';
        outputColumn: 'Temperatur';
        use: CelsiusToFahrenheit;
    }

    block BatterieTemperatureTableTransformer oftype TableTransformer {
        inputColumn: 'Batterietemperatur';
        outputColumn: 'Batterietemperatur';
        use: CelsiusToFahrenheit;
    }

    block TempSQLiteLoader oftype SQLiteLoader {
        table: "temperatures";
        file: "./temperatures.sqlite";
    }

    TemperatureExtractor
        ->TempArchiveInterpreter
        ->TempFilePicker
        ->TempFileInterpreter
        ->TempCSVInterpreter
        ->TempColumnDeleter
        ->TempTableInterpreter
        ->TemperatureTableTransformer
        ->BatterieTemperatureTableTransformer
        ->TempSQLiteLoader;
}